plugins {
    id('com.manning.gia.commons')
    id('maven-publish')
}

ext {
    mavenRepoUrl = "$mavenBaseUrl/sansovo-demo"
    mavenSnapshotRepoUrl = "$mavenBaseUrl/dearall-snapshots"
    mavenReleaseRepoUrl = "$mavenBaseUrl/dearall-releases"

    artifactoryUsername = project.hasProperty('maven_publish_userName') ? project.getProperty('maven_publish_userName') : null
    artifactoryPassword = project.hasProperty('maven_publish_userPassword') ? project.getProperty('maven_publish_userPassword') : null
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId project.name

            artifact sourcesJar
            artifact javadocJar

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = "Gradle todo web project"
                description = 'Gradle plugin for managing applications and databases on CloudBees RUN@cloud.'
                url = "https://github.com/dearall/todo-demo.git"
                inceptionYear = '2021'
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = "johnd"
                        name = "John Doe"
                        email = "john.doe@example.com"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/dearall/todo-demo.git"
                    developerConnection = "scm:git:https://github.com/dearall/todo-demo.git"
                    url = "https://github.com/dearall/todo-demo.git"
                }
            }
        }
    }
    repositories {
        /*
        maven {
            name 'myLocal'
            url "file://$projectDir/repo"
        }*/
        maven {
            name 'sansovoMaven'
//            url project.version.toString().endsWith('-SNAPSHOT') ? mavenSnapshotRepoUrl : mavenReleaseRepoUrl
            url mavenRepoUrl

            credentials {
                username = artifactoryUsername
                password = artifactoryPassword
            }

            allowInsecureProtocol = true
        }
    }
}
